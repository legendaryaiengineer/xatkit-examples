Library GithubBot

// Intents to assign labels and developers to new users in the repo (the new user event is notified automatically on the Slack channel 
intent GetIssue { 
	inputs {
		"Get issue IssueNumber"
	}
	creates context issue {
		sets parameter issueNumber from fragment IssueNumber (entity number)
	}
}

intent SetLabel {
	requires context issue
	inputs {
		"Set label Label"
		"Give label Label"
	}
	creates context  issue {
		sets parameter issueLabel from fragment Label (entity any)
	}
}

intent AssignUser {
	requires context issue
	inputs {
		"Assign user Username"
		"Username will take care of it"
	}
	creates context  issue {
		sets parameter assignedUsername from fragment Username (entity any)
	}
}


// Set of intents to chat with the user in order to collect the info to create a complete bug report that will save time to the maintainers trying to understand the error
intent OpenBug {
	inputs {
		"The plugin is not working"
		"I have a problem with the plugin"
		"I'd like to report an error"
		"I want to open a bug"
		"I want to report a bug"
		"There is an error in the plugin"
	}
	
}

intent DescribeBug follows OpenBug {
	inputs {
		"My error is Error"
		"The problem is Error"
		"I get this error: Error"
		"My error is that Error"
		"The problem is that Error"
		"I get the error Error"
	}
	creates context bug {
		sets parameter title from fragment Error (entity any)
	}
}

//as before, we assume the repo is a WordPress plugin and therefore we always need to know the WP and PHP version the user is running to locate the potential problemç
intent TellWPVersion follows DescribeBug {
	requires context bug
	inputs {
		"My version number is WPVersion"
		"I use number WPVersion"
		"It's version WPVersion"
	}
	creates context bug {
		sets parameter wpversion from fragment WPVersion (entity number)
	}
}

intent TellPHPVersion follows TellWPVersion {
	requires context bug
	inputs {
		"My version is PHPVersion"
		"I use PHPVersion"
		"It's version PHPVersion"
		"The server is on php version PHPVersion"
	}
	creates context bug {
		sets parameter phpversion from fragment PHPVersion (entity number)
	}
}



