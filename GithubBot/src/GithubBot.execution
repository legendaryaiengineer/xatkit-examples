import library "GithubBot/src/GithubBot.intent" as GithubBotLib
import platform "GithubPlatform"
import platform "SlackPlatform" 
import library "CoreLibrary"

use provider SlackPlatform.SlackIntentProvider
use provider GithubPlatform.GithubWebhookEventProvider

on event Issue_Opened do
	SlackPlatform.PostMessage("An issue has been opened with the title " + context.get("issue").get("issue->title"), config.get("slack.channel") as String)
	val issue = GithubPlatform.GetIssue(config.get("github.repository.username") as String, config.get("github.repository.name") as String, context.get("issue").get("issue->number") as String)
	session.put("issue", issue)
	
on intent GetIssue do 
	val com.jcabi.github.Issue issue = GithubPlatform.GetIssue(config.get("github.repository.username") as String, config.get("github.repository.name") as String, context.get("issue").get("issueNumber") as String)
	session.put("issue", issue)
	SlackPlatform.Reply("Found issue " + issue.number())
	
on intent SetLabel do
	GithubPlatform.SetLabel(session.get("issue") as com.jcabi.github.Issue, context.get("issue").get("issueLabel") as String)
	SlackPlatform.Reply("Done!")
	
on intent AssignUser do
	GithubPlatform.AssignUser(session.get("issue") as com.jcabi.github.Issue, context.get("issue").get("assignedUsername") as String)
	SlackPlatform.Reply("Done!")
	
on intent OpenBug do
	SlackPlatform.Reply("Can you please describe what is wrong in a few words?")
	
on intent DescribeBug do
	SlackPlatform.Reply("Can you let us know the WordPress version you're using?")
	
on intent TellWPVersion do
	SlackPlatform.Reply("Can you let us know the PHP version you're using?")
		
on intent TellPHPVersion do
	SlackPlatform.Reply("Thanks for your detailed info")	
	val com.jcabi.github.Issue newissue = GithubPlatform.OpenIssue(config.get("github.repository.username") as String, config.get("github.repository.name") as String, context.get("bug").get("title") as String, 
			"WP version is " + context.get("bug").get("wpversion") as String + " PHP version is " + context.get("bug").get("phpversion") as String
	)
	GithubPlatform.SetLabel(newissue, "bug")
	
on intent Default_Fallback_Intent do
	SlackPlatform.Reply("Sorry I didn't get it")
	
	
		
/* on intent CreateBug do
	action GithubPlatform.OpenIssue(user : "jcabot", repository : "xatkit-tests", issueTitle : context(bug).get("title") , issueContent : "")
	action GithubPlatform.SetLabel(issue : session.get("issue"), label : context(issue).get("issueLabel"))
	
	
	action SlackPlatform.Reply(message : "Done!")
*/		